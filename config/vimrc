"      .-.     .-.     .-.     .-.     .-.     .-.     .-.
" `._.'   `._.'   `._.'   `._.'   `._.'   `._.'   `._.'
"
" https://github.com/rafi/vim-config

" Runtime and Plugins
" -------------------------------------------------

if &compatible
	set nocompatible
endif

" Set main configuration directory, and where cache is stored.
let $VIMPATH = fnamemodify(resolve(expand('<sfile>:p')), ':h:h')
let $VARPATH = expand(($XDG_CACHE_HOME ? $XDG_CACHE_HOME : '~/.cache').'/vim')

function! s:dein_load_yaml(filename) abort
	if executable('yaml2json') && exists('*json_decode')
		" Decode YAML using the CLI tool yaml2json
		" See: https://github.com/koraa/large-yaml2json-json2yaml
		let g:denite_plugins = json_decode(
					\ system('yaml2json', readfile(a:filename)))
	else
		" Fallback to use python and PyYAML
	python << endpython
import vim, yaml
with open(vim.eval('a:filename'), 'r') as f:
	vim.vars['denite_plugins'] = yaml.load(f.read())
endpython
	endif

	for plugin in g:denite_plugins
		call dein#add(plugin['repo'], extend(plugin, {}, 'keep'))
	endfor
	unlet g:denite_plugins
endfunction

function! s:source_file(path) abort
	execute 'source' fnameescape($VIMPATH.'/config/'.a:path)
endfunction

function! s:lazy_load_filetype() abort
	if execute('filetype') =~# 'OFF'
		silent! filetype plugin indent on
		syntax enable
		filetype detect
	endif
endfunction

" Set augroup
augroup MyAutoCmd
	autocmd!
	" Lazy-loading filetype syntax and highlighting
	autocmd FileType,Syntax,BufNewFile,BufNew,BufRead *?
		\ call s:lazy_load_filetype()
augroup END

" When running n/vim with -S (source session) argument, the first buffer
" isn't type-detected, fix it by reloading the windows in the first tab.
augroup session-load-fix
	autocmd!
	autocmd SessionLoadPost *
		\ if ! exists('s:session_ft')
		\ |  let s:session_ft = timer_start(1, 'OnSessionLoadPost', {'repeat': 1})
		\ | endif
augroup END

function! OnSessionLoadPost(timer)
	" Store the current window and tab index
	let [tabnr, winnr, altwinnr] = [tabpagenr(), winnr(), winnr('#')]
	" Reload windows in the first tab
	tabn 1 | windo if empty(&ft) | edit | endif
	" Restore alternate window, current window and tab
	execute 'tabn '.tabnr.' | '.altwinnr.'wincmd w | '.winnr.'wincmd w'
	" Clear augroup
	augroup session-load-fix | autocmd!
	augroup END
endfunc

" Initialize base requirements
if has('vim_starting')
	call s:source_file('init.vim')
	if has('nvim')
		call s:source_file('neovim.vim')
	endif
endif

" Initialize dein.vim (package manager)
let s:path = expand('$VARPATH/dein')
let s:plugins_path = expand('$VIMPATH/config/plugins.yaml')
if dein#load_state(s:path)
	call dein#begin(s:path, [expand('<sfile>'), s:plugins_path])
	try
		call s:dein_load_yaml(s:plugins_path)
	catch /.*/
		echoerr v:exception
		echomsg 'Error loading config/plugins.yaml...'
		echomsg 'Caught: ' v:exception
		echoerr 'Please run: pip install --user PyYAML'
	endtry

	if isdirectory(expand('$VIMPATH/dev'))
		call dein#local(expand('$VIMPATH/dev'), {'frozen': 1, 'merged': 0})
	endif
	call dein#end()
	call dein#save_state()
	if dein#check_install()
		if ! has('nvim')
			set nomore
		endif
		call dein#install()
	endif
endif

call s:source_file('plugins/all.vim')
call s:source_file('theme.vim')

if has('vim_starting') && ! empty(argv())
	call s:lazy_load_filetype()
endif

if ! has('vim_starting')
	call dein#call_hook('source')
	call dein#call_hook('post_source')

	filetype plugin indent on
	syntax enable
endif

" Loading configuration modules {{{
call s:source_file('general.vim')
call s:source_file('terminal.vim')
call s:source_file('filetype.vim')
call s:source_file('mappings.vim')
" }}}

set secure
" vim: set ts=2 sw=2 tw=80 noet :
